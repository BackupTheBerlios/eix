#compdef eix-remote
local curcontext="$curcontext" state state_descr line
typeset -A opt_args
_arguments -C -s -S -A '-*' : \
'(1 -)'{'-h','-\\?'}'[help]' \
'-k[keep permissions]' \
'-K[keep permissions, resistent to -i]' \
'-l+[PATH (use PATH as LOCAL_LAYMAN)]:LOCAL_LAYMAN:_files -/' \
'-a+[ADDR (use ADDR as remote database)]:remote address: ' \
'-i[ignore all previous options]' \
'-v[verbose (default)]' \
'-H[no status line update]' \
'-q[quiet]' \
'1:commands:->cmds'
local ret=$?
case $state in
(cmds)
	local cmds
	cmds=(
	'update:Fetch eix-caches and add them to the eix database'
	'add:Add fetched eix-caches to the eix database'
	'fetch:Fetch eix-caches'
	'remove:remove temporarily added virtual overlays from eix database'
	)
	_describe -t commands 'command' cmds
	ret=$?;;
esac
return ret
