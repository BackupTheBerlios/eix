#compdef eix-installed-after
local stat
_eix_installed_after_arg() {
	local d
	local -x NEWLINE=false CAREMODE=false QUICKMODE=true \
		RESTRICT_INSTALLED=false USE_BUILD_TIME=false \
		FORMAT='<installedversions:I>' I
	if (( $words[(i)(-*[lLV]*)] < CURRENT ))
	then	d='-version'
		I='-<version>'
	else	d='[:slot]'
		I='{slots}:<slot>{}'
	fi
	I='<category>/<name>'"$I"'\n'
	_wanted package expl 'category/package'"$d" compadd -- $(eix '-I*' 2>/dev/null)
}
_arguments -s -S -A '-*' : \
'(1 -)'{'-h','-H','-\\?'}'[help]' \
'(1 -b -L -l -a -A -i -e -S -V)-f[output full list of packages]' \
'-b[output packages installed before (not after) the package]' \
'-i[include the given package in the output]' \
'(-L)-l[use /var/log/emerge.log to determine first install time]' \
'(-l)-L+[LOG (use LOG to determine first install time)]:scan logfile:_files' \
'-e[use date as argument, not a package name]' \
'-a+[SEC (increase match for -l or -L or -e by SEC seconds)]:increase seconds: ' \
'-A+[SEC (decrease match for -l or -L or -e by SEC seconds)]:decrease seconds: ' \
'-S[choose first matching slot]' \
'-V[look for category/package-version, not category/package\:slot]' \
'-d[output installation date after package]' \
'-s[output slot also in case of no ambiguity]' \
'-v[output category/package-version, not category/package\:slot]' \
'-\=[output \=category/package-version, not category/package\:slot]' \
'-t[use build time independent of USE_BUILD_TIME]' \
'-T[never use build time independent of USE_BUILD_TIME]' \
'1:package: _eix_installed_after_arg'
stat=$?
unset -f _eix_installed_after_arg
return stat
