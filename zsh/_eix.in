#compdef eix eix-diff eix-update
local -a service_opts local_functions
local excl_opt stat
local_functions=(_eix__vars)
_eix__vars() {
	compadd "${@}" -- ${(f)"$(eix --known-vars)"}
}

case $service in
(*diff*)
	:
;;
(*)
	local_functions+=(_eix__overlay)
_eix__overlay() {
	local -aU o
	o=($(PRINT_APPEND='' eix --print PORTDIR_OVERLAY 2>/dev/null))
	o+=("$(PRINT_APPEND='' eix --print PORTDIR 2>/dev/null)")
	o+=({0..$#o})
	compadd "${@}" -- $o
}
;;
esac

service_opts=(
{'(--quick)-Q','(-Q)--quick'}'[do not try to read unguessable slots]'
'--care[always read slots of installed packages]'
)

case $service in
(*update*)
	excl_opt='(-)'
	service_opts=(
{'(--output)-o+','(-o)--output'}'[output to FILE]:output_file:_files'
{'*--exclude-overlay','*-x+'}'[OVERLAY (exclude)]:exclude overlay:_eix__overlay'
{'*--add-overlay','*-a+'}'[OVERLAY (add)]:add overlay:_files -/'
{'*--override-method','*-m+'}'[OVERLAY_MASK METHOD (override method)]:overlay mask to change method:_eix__overlay:cache method: '
{'*--repo-name','*-r+'}'[OVERLAY REPO_NAME (set REPO_NAME for OVERLAY)]:overlay to change name:_eix__overlay:repo-name: '
)
;;
(*diff*)
	excl_opt='(1 2 -)'
	service_opts+=(
'1:old_cache:_files'
'2::new_cache:_files'
)
;;
(*)
	excl_opt='(* -)'
	service_opts+=(
"$excl_opt"'--print-overlay-path[OVERLAY (print path for OVERLAY)]:overlay for printing path:_eix__overlay'
"$excl_opt"'--print-overlay-label[OVERLAY (print label for OVERLAY)]:overlay for printing label:_eix__overlay'
"$excl_opt"'--print-all-useflags[print all IUSE words]'
"$excl_opt"'--print-all-keywords[print all KEYWORDS words]'
"$excl_opt"'--print-all-slots[print all SLOT strings]'
"$excl_opt"'--print-all-provides[print all PROVIDE words]'
"$excl_opt"'--print-all-licenses[print all LICENSE strings]'
"$excl_opt"'--print-all-print-world-sets[print the world sets]'
"$excl_opt"'--print-all-print-world-sets[print the world sets]'
"$excl_opt"'--is-current[is @EIX_CACHEFILE@ readable?]'
{'(--versionsort)-x','(-x)--versionsort'}'[sort output by slots/versions]'
{'(--versionlines)-l','(-l)--versionlines'}'[output versions line by line]'
{'(--compact)-c','(-c)--compact'}'[use \$FORMAT_COMPACT]'
{'(--verbose)-v','(-v)--verbose'}'[use \$FORMAT_VERBOSE]'
'--xml[output in xml format]'
{'(--pure-packages)-\\*','(-\\*)--pure-packages'}'[omit printing of overlay names and package number]'
'--only-names[print with format \<category\>/\<name\>]'
{'(--brief)-0','(-0)--brief'}'[print at most one package]'
'--brief2[print at most two packages]'
{'(--test-non-matching)-t','(-t)--test-non-matching'}'[check /etc/portage/package.* and installed packages]'
'--cache-file[CACHE_FILE (use instead of @EIX_CACHEFILE@)]:cache-file:_files'
'--format[FORMAT]:format: '
'--format-compact[FORMAT_COMPACT]:format_compact: '
'--format-verbose[FORMAT_VERBOSE]:format_verbose: '
{'*--not','*-\!'}'[logical negation]'
{'*--and','*-a'}'[logical disjunction]'
{'*--or','*-o'}'[logical conjunction]'
{'*--open','*-\('}'[logical opening brace]'
{'*--close','*-\)'}'[logical closing brace]'
{'*--installed','*-I'}'[test for installed packages]'
{'*--multi-installed','*-i'}'[test for multiple installed packages]'
{'*--dup-packages','*-d'}'[test for duplicate packages]'
{'*--dup-versions','*-D'}'[test for duplicated versions]'
{'*--slotted','*-1'}'[test for nontrivial slots]'
{'*--slots','*-2'}'[test for multiple slots]'
{'*--upgrade','*-u'}'[test for upgradable packages]'
'*--upgrade\+[test for upgradable packages, local]'
'*--upgrade\-[test for upgradable packages, non-local]'
'*--stable[test for stable packages]'
'*--stable+[test for stable packages, local]'
'*--stable-[test for stable packages, non-local]'
'*--testing[test for testing packages]'
'*--testing+[test for testing packages, local]'
'*--testing-[test for testing packages, non-local]'
'*--non-masked[test for non-masked packages]'
'*--non-masked+[test for non-masked packages, local]'
'*--non-masked-[test for non-masked packages, non-local]'
'*--system[test for @system/virtual @system packages]'
'*--system+[test for @system/virtual @system packages, local]'
'*--system-[test for @system/virtual @system packages, non-local]'
'*--system-plain[test for non-virtual @system packages]'
'*--system-plain+[test for non-virtual @system packages, local]'
'*--system-plain-[test for non-virtual @system packages, non-local]'
'*--installed-unstable[test for installed unstable packages]'
'*--installed-testing[test for installed testing packages]'
'*--installed-masked[test for installed masked packages]'
'*--world[test for @world/virtuals for @world packages]'
'*--world-plain[test for non-virtual @world packages]'
'*--world-file[test for @world-file/virtuals for @world-file packages]'
'*--world-file-plain[test for non-virtual @world-file packages]'
'*--world-set[test for @world-set/virtuals for @world-set packages]'
'*--world-set-plain[test for non-virtual @world-set packages]'
'*--selected[test for @selected/virtuals for @selected packages]'
'*--selected-plain[test for non-virtual @selected packages]'
'*--selected-file[test for @selected-file/virtuals for @selected-file packages]'
'*--selected-file-plain[test for non-virtual @selected-file packages]'
'*--selected-set[test for @selected-set/virtuals for @selected-set packages]'
'*--selected-set-plain[test for non-virtual @selected-set packages]'
'*--binary[test for packages with .tbz2 file]'
{'*--overlay','*-O'}'[test if package is in an overlay]'
'*--in-overlay[OVERLAY (test for packages in OVERLAY)]:overlay for testing:_eix__overlay'
'*--only-in-overlay[OVERLAY (test for packages only in OVERLAY)]:overlay (only) for testing:_eix__overlay'
{'*--installed-overlay','*-J'}'[test if package is installed from an overlay]'
'*--installed-from-overlay[OVERLAY (test for package installed from OVERLAY)]:overlay (installed) for testing:_eix__overlay'
'*--installed-in-overlay[OVERLAY (test for package installed and in OVERLAY)]:overlay (installed and in) for testing:_eix__overlay'
'*--installed-in-some-overlay[test for package installed and in some overlay]'
'*--restrict-fetch[test for RESTRICT=fetch packages]'
'*--restrict-mirror[test for RESTRICT=mirror packages]'
'*--restrict-primaryuri[test for RESTRICT=primaryuri packages]'
'*--restrict-binchecks[test for RESTRICT=binchecks packages]'
'*--restrict-strip[test for RESTRICT=strip packages]'
'*--restrict-test[test for RESTRICT=test packages]'
'*--restrict-userpriv[test for RESTRICT=userpriv packages]'
'*--restrict-installsources[test for RESTRICT=installsources packages]'
'*--restrict-bindist[test for RESTRICT=bindist packages]'
'*--restrict-parallel[test for RESTRICT=parallel packages]'
'*--properties-interactive[test for PROPERTIES=interactive packages]'
'*--properties-live[test for PROPERTIES=live packages]'
'*--properties-virtual[test for PROPERTIES=virtual packages]'
'*--properties-set[test for PROPERTIES=set packages]'
{'*--test-obsolete','*-T'}'[test for packages with obsolete /etc/portage/package.*]'
{'*--pipe','*-\|'}'[test for packages matching stdin]'
{'*--name','*-s'}'[match name]'
{'*--description','*-S'}'[match description]'
{'*--category','*-C'}'[match category]'
{'*--category-name','*-A'}'[match "category/name"]'
{'*--homepage','*-H'}'[match homepage]'
{'*--license','*-L'}'[match license]'
{'*--provide','*-P'}'[match provide]'
{'*--use','*-U'}'[match IUSE]'
'*--set[match setname]'
'*--slot[match slotname]'
'*--installed-slot[match installed slotname]'
'*--installed-with-use[match installed enabled USE]'
'*--installed-without-use[match installed disabled USE]'
{'*--exact','*-e'}'[pattern is exact string]'
{'*--begin','*-b'}'[pattern is beginning of string]'
'*--end[pattern is end of string]'
{'*--substring','*-z'}'[pattern is substring]'
{'*--fuzzy','*-f+'}'[N (pattern is fuzzy, Levenshtein N)]::number (Levenshtein distance):(1 2 3 4 5 6 7 8 9)'
{'*--pattern','*-p'}'[pattern is wildcard pattern]'
{'*--regex','*-r'}'[pattern is regular expression]'
)
# find out search field and whether to check only installed packages
	local field='' installed=''
	if (( CURRENT>2 ))
	then	local i c w is_or=false
		case $(PRINT_APPEND='' eix --print DEFAULT_IS_OR 2>/dev/null) in
		([yY1:tT]*|[oO][nN]*)
			is_or=:
		;;
		esac
		for (( i=2 ; i<CURRENT ; ++i))
		do	case $words[$i] in
			(--)
				i=$CURRENT
			;;
			(--print-overlay-(path|label)|--cache-file|--format(-compact|-verbose|)|--(only-|)in-overlay|--installed-(from|in)-overlay|--fuzzy|--print)
				((++i))
			;;
			(--not|--open|--close|---*|[!-])
				if $is_or
				then	field=''
					installed=''
				fi
			;;
			(--(multi-|)installed)
				installed='installed'
			;;
			(--category|--name|--description|--homepage|--license|--provide)
				field="${words[$i]#--}"
			;;
			(--category-name)
				field='category/name'
			;;
			(--use|--installed-with(out|)-use)
				field='useflag'
			;;
			(--set)
				field='set'
			;;
			(--slot)
				field='slot'
			;;
			(--installed-slot)
				installed='installed'
				field='slot'
			;;
			(-[!-]*)
				w=$words[$i]
				for (( c=2 ; c<=$#w ; ++c ))
				do	case $w[$c] in
					([f])
						if [[ $c = $#w ]]
						then	((++i))
						else	c=$#w
						fi
					;;
					([\(\)!])
						if $is_or
						then	field=''
							installed=''
						fi
					;;
					([iI])
						installed='installed'
					;;
					(C)
						field='category'
					;;
					(s)
						field='name'
					;;
					(S)
						field='description'
					;;
					(A)
						field='category/name'
					;;
					(H)
						field='homepage'
					;;
					(L)
						field='license'
					;;
					(P)
						field='provide'
					;;
					esac
				done
			;;
			esac
		done
	fi
	local -a modes
	modes=(${installed} ${field:-'package'})
	service_opts+=(
'*:'"$modes"':_eix__arg -- '"$modes"
)
	local_functions+=(_eix__arg)
_eix__arg() {
	local -a addarg
	addarg=($@[0,(R)(--)])
	shift $#addarg
	local -a eixopt
	eixopt=('-*')
	if [[ $1 == installed ]]
	then	shift
		eixopt+=('-I')
	fi
	local -x NEWLINE=false CAREMODE=false QUICKMODE=true \
		RESTRICT_INSTALLED=false USE_BUILD_TIME=false
	case $1 in
	(category)
		compadd $addarg $(eix $eixopt --format '<category>\n' 2>/dev/null)
	;;
	(name)
		compadd $addarg $(eix $eixopt --format '<name>\n' 2>/dev/null)
	;;
	(category/name)
		compadd $addarg $(eix $eixopt --only-names 2>/dev/null)
	;;
	(package)
		case $PREFIX in
		(*/*)
			compset -P '*/'
			compadd $addarg $(eix $eixopt -PC "${IPREFIX%/}" --format '<name>\n' 2>/dev/null)
		;;
		(*)
			local -a cat_name
			cat_name=($(eix $eixopt --only-names 2>/dev/null))
			compadd $addarg ${cat_name:t}
			compadd $addarg[1,-2] -S '/' -- ${cat_name%%/*}
		;;
		esac
	;;
	(useflag)
		compadd $addarg ${$(eix --print-all-useflags 2>/dev/null)##[()+-]#}
	;;
	(license)
		compadd $addarg ${$(eix --print-all-licenses 2>/dev/null)//([\(\)\|]|[![:space:]]#\?)/}
	;;
	(slot)
		compadd $addarg $(eix --print-all-slots 2>/dev/null)
	;;
	(provide)
		compadd $addarg ${$(eix --print-all-provides 2>/dev/null)//([\(\)\|]|[![:space:]]#\?)/}
	;;
	(homepage)
		compadd $addarg $(eix $eixopt --format '<homepage>\n' 2>/dev/null)
	;;
	(set)
		local -aU o
		o=($(PRINT_APPEND='' eix --print PORTDIR_OVERLAY 2>/dev/null))
		local i j k portdir="$(PRINT_APPEND='' eix --print PORTDIR 2>/dev/null)"
		local -a sets
		sets=()
		for i in ${="$(PRINT_APPEND='' eix --print EIX_LOCAL_SETS 2>/dev/null)"}
		do	case $i in
			(/*)
				[[ -d $i ]] && sets+=($i)
			;;
			('*'*)
				k=${i#'*'}
				for j in $o
				do	[[ -d "$j$k" ]] && sets+=("$j$k")
				done
			;;
			(*)	[[ -d "$portdir$i" ]] && sets+=("$portdir$i")
			;;
			esac
		done
		(( $#sets )) && \
			compadd $addarg ${${(f)"$(find -L $sets -type f -name '[!.]*' -print 2>/dev/null)"}:t}
	;;
	esac
}
;;
esac

_arguments -s -S : \
"$excl_opt"{'--help','-h'}'[help]' \
"$excl_opt"{'--version','-v'}'[version]' \
"$excl_opt"'--print[print expanded variable value]:variable:_eix__vars' \
"$excl_opt"'--dump[dump variables]' \
"$excl_opt"'--dump-defaults[dump default values of variables]' \
{'(--quiet)-q','(-q)--quiet'}'[no output]' \
'(--forcecolor -F '{'--nocolor)-n','-n)--nocolor'}'[do not use colors in output]' \
'(--nocolor -n '{'--forcecolor)-F','-F)--forcecolor'}'[force color on non-terminal]' \
$service_opts
stat=$?
unset -f $local_functions
return stat
